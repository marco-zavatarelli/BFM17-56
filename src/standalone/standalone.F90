#include "cppdefs.h"
!-----------------------------------------------------------------------
!BOP
!
! !MODULE: standalone
!
! !INTERFACE:
   module standalone
!
! !DESCRIPTION:
! This module contains all the routines for the standard BFM
! simulation in standalone version, i.e. with a 0.5D setup
! (pelagic and benthic).
! It also contains all the ancillary functions for forcing functions.
!
! !USES:
!  default: all is private.
   use global_mem, only:RLEN,ONE
   use constants,  only:SEC_PER_DAY
   private
!
! !PUBLIC MEMBER FUNCTIONS:
   public timestepping,init_standalone,end_standalone

! !PUBLIC DATA MEMBERS:
   ! Note: all read from namelist
   !---------------------------------------------
   ! geographic and dimensional parameters
   !---------------------------------------------
   real(RLEN),public        :: latitude,longitude
   integer, public          :: nboxes
   real(RLEN),public        :: indepth
   !---------------------------------------------
   ! timestepping parameters
   ! real:
   ! mindelt: minimal time step allowed for computation
   ! endtim: endtime of integration
   ! time: actual time
   ! delt: actual time step of global integration
   ! maxdelt: maximal timestep
   !---------------------------------------------
   real(RLEN),public  :: maxdelt,mindelt,endtim, &
                         timesec,delt
   !---------------------------------------------
   ! integer:
   ! nmaxdelt: number of mindelts in maxdelts
   ! nendtim: number of total maxelts to endtim
   ! nmin: actual no. of mindelts in maxdelt intervall
   ! nstep: actual time step in mindelts
   ! ntime: actual time in maxdelts
   ! method: integration method
   !---------------------------------------------
   integer,public     :: nmaxdelt,nendtim,nmin,nstep,ntime, &
                         method
   !---------------------------------------------
   ! arrays for integration routines
   !---------------------------------------------
   real(RLEN),public,dimension(:,:),allocatable :: bbccc3D,bccc3D,ccc_tmp3D
   real(RLEN),public,dimension(:,:),allocatable :: bbccc2D,bccc2D,ccc_tmp2D
   real(RLEN),public                            :: dtm1
   logical,public                               :: sspflag

!
! !PRIVATE DATA MEMBERS:
   real(RLEN),parameter :: PI=3.14159265,RFACTOR=PI/180.
   integer,parameter    :: namlst=10,unit=11

!
! !REVISION HISTORY:
!  Original author(s): Marcello Vichi (INGV), Momme Buthenschoen (UNIBO)
!
!  $Log: $
!EOP
!-----------------------------------------------------------------------

   contains

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Initialise the standalone BFM
!
! !INTERFACE:
   subroutine init_standalone()
!
! !DESCRIPTION:
!  Main communication of array dimensions between
!  BFM and the standalone setup
!  Read also integration limits
!
!
! !USES:
   use constants, only: E2W
   use mem,   only: NO_D3_BOX_STATES, NO_BOXES,          &
                  NO_BOXES_X, NO_BOXES_Y, NO_BOXES_Z,  &
                  NO_D2_BOX_STATES, NO_BOXES_XY,       &
                  NO_D2_BOX_DIAGNOSS, NO_D3_BOX_DIAGNOSS,&
                  NO_STATES,Depth, D3STATE
   use mem,  only: Volume, Area, Area2d
   use global_mem, only:RLEN,LOGUNIT,NML_OPEN,NML_READ,error_msg_prn
   use api_bfm
   use netcdf_bfm, only: init_netcdf_bfm,init_save_bfm,&
                         init_netcdf_rst_bfm,read_rst_bfm
   use time
#ifdef INCLUDE_BEN
   use mem, only: D2STATE, Depth_ben
#endif

   IMPLICIT NONE
!
! !INPUT PARAMETERS:
!
! !REVISION HISTORY:
!  Original author(s): Marcello Vichi
!
! !LOCAL VARIABLES:
   namelist /standalone_nml/ nboxes,indepth,maxdelt,    &
            mindelt,endtim,method,latitude,longitude
   namelist /time_nml/ timefmt,MaxN,start,stop,simdays
!
! !LOCAL VARIABLES:
   real(RLEN) :: tt
   integer    :: dtm1,i
   character(LEN=8)  :: datestr
!EOP
!-----------------------------------------------------------------------
!BOC
   call Date_And_Time(datestr,timestr)
   STDERR LINE
   STDERR 'BFM standalone started on  ',datestr,' ',timestr
   STDERR LINE
   LEVEL1 'init_standalone'
   !---------------------------------------------
   ! Give initial default values
   ! (overwritten with namelist)
   !---------------------------------------------
   nboxes      = 1
   indepth     = 10.0
   latitude    = 0.0
   longitude   = 0.0
   maxdelt     = 100.0
   mindelt     = 1.0
   endtim      = 360.0
   method      = 1

   open(namlst,file='standalone.nml',status='old',action='read',err=100)
   read(namlst,nml=standalone_nml,err=101)
   close(namlst)
   open(namlst,file='standalone.nml',status='old',action='read',err=100)
   read(namlst,nml=time_nml,err=103)
   close(namlst)

   !---------------------------------------------
   ! set the dimensions
   !---------------------------------------------
#ifdef INCLUDE_BENPROFILES
   ! dirty method to cheat the standalone model
   NO_BOXES_X  = 1
   NO_BOXES_Z  = nboxes
#else
   NO_BOXES_X  = nboxes
   NO_BOXES_Z  = 1
#endif
   NO_BOXES_Y  = 1
   NO_BOXES    = NO_BOXES_X * NO_BOXES_Y * NO_BOXES_Z
   NO_BOXES_XY = NO_BOXES_X * NO_BOXES_Y
   NO_STATES   = NO_D3_BOX_STATES * NO_BOXES +   &
                 NO_D2_BOX_STATES * NO_BOXES_XY
   LEVEL3 'Number of Boxes:',nboxes
   LEVEL3 'Box Depth:',indepth
   ! set where surface and bottom boxes are 
   ! (actually all boxes in standalone mode)
   allocate(SRFindices(NO_BOXES_XY))
   allocate(BOTindices(NO_BOXES_XY))
   SRFindices = (/(i,i=1,NO_BOXES_XY)/)
   BOTindices = (/(i,i=1,NO_BOXES_XY)/)

   !---------------------------------------------
   ! initialise the timestepping parameters
   ! Use the GOTM time-manager
   ! Time is given in Julian day and seconds of day
   ! (both integer values)
   !---------------------------------------------
   timestep = maxdelt
   call init_time(MinN,MaxN)
   if (HasRealTime) then
      timesec=julianday*SEC_PER_DAY+secondsofday
      simdays=nint(simtime/SEC_PER_DAY)
   else
      timesec=ZERO
   end if
   nmaxdelt=1
   LEVEL3 'nmaxdelt: ',nmaxdelt
   tt=maxdelt/2.
   do while (tt.ge.mindelt)
      tt=tt/2.
      nmaxdelt=nmaxdelt*2
      LEVEL3 'nmaxdelt: ',nmaxdelt
   end do
   mindelt=maxdelt/nmaxdelt ! maxdelt = nmaxdelt*mindelt
   nendtim=MaxN
   nstep=nmaxdelt
   ntime=0
   nmin=0
   dtm1=maxdelt
   delt=maxdelt
   if (method.eq.3) delt=2.0_RLEN*delt
   LEVEL3 'Integration method: ',method
   LEVEL3 'maxdelt (sec): ',maxdelt
   LEVEL3 'mindelt (sec): ',mindelt
   LEVEL3 'nmaxdelt: ',nmaxdelt
   LEVEL3 'Simulation time (days): ',simdays
   LEVEL3 'nendtim: ',nendtim
   LEVEL3 'Initial time (sec): ',timesec

   !---------------------------------------------
   ! Initialise the BFM with standalone settings
   !---------------------------------------------
   call init_bfm(namlst)
   !---------------------------------------------
   ! Initialise state variable names and diagnostics
   !---------------------------------------------
   call set_var_info_bfm
   !---------------------------------------------
   ! Allocate memory and give initial values
   ! to the pelagic system
   !---------------------------------------------
   ! the argument list is kept for compatibility 
   ! with GOTM
   call init_var_bfm(namlst,'bfm.nml',unit,bio_setup)
   !---------------------------------------------
   ! Assign depth
   !---------------------------------------------
   Depth = indepth
#ifdef INCLUDE_BEN
   Depth_ben = Depth
#endif
   ! assume area is 1m^2 (make a parameter in the future for 
   ! mesocosm simulations)
   Area = ONE
   Area2d = ONE
   Volume = Depth*Area
   !---------------------------------------------
   ! Initialise external forcing functions
   !---------------------------------------------
   call init_envforcing_bfm

#ifdef INCLUDE_BEN
   !---------------------------------------------
   ! Initialise the benthic system
   ! Layer depths and pore-water nutrients are initialised 
   ! according to the value of calc_initial_bennut_states
   !---------------------------------------------
   call init_benthic_bfm(namlst,'bfm.nml',unit,bio_setup)
#endif
#ifdef INCLUDE_SEAICE
   !---------------------------------------------
   ! Initialise the sea-ice system
   !---------------------------------------------
   call init_seaice_bfm(namlst,'seaice.nml',unit,bio_setup)
#endif

   !---------------------------------------------
   ! Read restart file (if flag)
   ! Overwrite previous initialization
   !---------------------------------------------
   if (bfm_init == 1) call read_rst_bfm(rst_fname)

   !---------------------------------------------
   ! Initialise the diagnostic variables
   !---------------------------------------------
   call CalcVerticalExtinction( )
   call CalcChlorophylla( )

   !---------------------------------------------
   ! Initialise netcdf output
   !---------------------------------------------
   call calcmean_bfm(INIT)
   call calcmean_bfm(ACCUMULATE)
   call init_netcdf_bfm(out_fname,start,0,  &
             lat=latitude,lon=longitude,z=Depth,   &
             oceanpoint=(/(i,i=1,NO_BOXES)/),      &
             surfacepoint=(/(i,i=1,NO_BOXES_XY)/), &
             bottompoint=(/(i,i=1,NO_BOXES_XY)/))
   call init_save_bfm
   call calcmean_bfm(RESET)
   !---------------------------------------------
   ! Initialise netcdf restart file
   !---------------------------------------------
   call init_netcdf_rst_bfm(rst_fname)

   !---------------------------------------------
   ! allocate and initialise integration arrays
   !---------------------------------------------
   allocate(bbccc3D(NO_D3_BOX_STATES,NO_BOXES))
   allocate(bccc3D(NO_D3_BOX_STATES,NO_BOXES))
   allocate(ccc_tmp3D(NO_D3_BOX_STATES,NO_BOXES))
   allocate(bbccc2D(NO_D2_BOX_STATES,NO_BOXES_XY))
   allocate(bccc2D(NO_D2_BOX_STATES,NO_BOXES_XY))
   allocate(ccc_tmp2D(NO_D2_BOX_STATES,NO_BOXES_XY))
   ! Initialize prior time step for leap-frog:
   if (method == 3) then
      bbccc3d = D3STATE
      ccc_tmp3D = D3STATE
#ifdef INCLUDE_BEN
      bbccc2d = D2STATE
      ccc_tmp2D = D2STATE
#endif
   end if

#ifdef DEBUG
   ! print out initial values (first grid point only)
   do i=1,NO_D3_BOX_STATES
     LEVEL1 trim(var_names(stPelStateS+i-1)),D3STATE(i,1)
   end do
   do i=1,NO_D2_BOX_STATES
     LEVEL1 trim(var_names(stBenStateS+i-1)),D2STATE(i,1)
   end do
#endif

   return

100   call error_msg_prn(NML_OPEN,"standalone.f90","standalone.nml")
101   call error_msg_prn(NML_READ,"standalone.f90","standalone_nml")
103   call error_msg_prn(NML_READ,"standalone.f90","time_nml")

   end subroutine init_standalone
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE:
!
! !INTERFACE:
   SUBROUTINE timestepping()
!
! !DESCRIPTION:
!
!
! !USES:
   use global_mem, only:RLEN
   use netcdf_bfm, only: save_bfm
   use mem
   use api_bfm, only: out_delta
   use time
   IMPLICIT NONE
! !INPUT PARAMETERS:
! !INPUT/OUTPUT PARAMETERS:
!
! !OUTPUT PARAMETERS:
! !REVISION HISTORY:
!  Author(s): Momme Butenschoen (UNIBO)
!
! !LOCAL VARIABLES:
!
!EOP
!-----------------------------------------------------------------------
!BOC
integer :: i

   LEVEL1 'timestepping'

   do while (ntime.le.nendtim)
#ifdef DEBUG
      LEVEL2 'ntime=',ntime
#endif
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      ! Compute environmental forcing functions
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      call envforcing_bfm
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      ! Compute extinction coefficient
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      call CalcVerticalExtinction( )
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      ! Compute reaction terms
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      call EcologyDynamics
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      ! Integrate forward in time
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      select case (method)
         case (2)
            call integrationRK2
         case (3)
            call integrationLf
         case default
            call integrationEfw
      end select
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      ! Compute means and store results
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      call calcmean_bfm(ACCUMULATE)
      if (mod(ntime,out_delta).eq.0) then
         LEVEL1 'OUTPUT' , timesec/SEC_PER_DAY
         call calcmean_bfm(MEAN)
         call save_bfm(timesec)
      end if
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      ! Reset source-sink arrays, update time
      !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      call ResetFluxes
      call update_time(ntime)
#ifdef DEBUG
      LEVEL2 'julian, seconds=',julianday,secondsofday
#endif
   end do

   END SUBROUTINE timestepping
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Finalise the standalone BFM
!
! !INTERFACE:
   subroutine end_standalone()
!
! !DESCRIPTION:
!  Terminates the standalone simulation.
!
!
! !USES:
   use time
   use netcdf_bfm, only: close_ncdf, ncid_bfm, save_rst_bfm, ncid_rst
   IMPLICIT NONE
! !INPUT PARAMETERS:
! !INPUT/OUTPUT PARAMETERS:
!
! !OUTPUT PARAMETERS:
! !REVISION HISTORY:
!  Author(s): Karsten Bolding and Hans Burchard
!
! !LOCAL VARIABLES:
   character(LEN=8)          :: datestr
!
!EOP
!-----------------------------------------------------------------------
!BOC
   ! save and close the restart file
   call save_rst_bfm
   call close_ncdf(ncid_rst)
   call close_ncdf(ncid_bfm)
   call end_envforcing_bfm
   call ClearMem
   call Date_And_Time(datestr,timestr)
   STDERR LINE
   STDERR 'BFM standalone finished on  ',datestr,' ',timestr
   end subroutine end_standalone
!EOC
!-----------------------------------------------------------------------

   END MODULE standalone

