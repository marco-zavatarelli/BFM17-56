#include "cppdefs.h"

#ifdef INCLUDE_PELCO2
!-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
! MODEL  BFM - Biogeochemical Flux Model 
!-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
!BOP
!
! !ROUTINE: ModulePelCO2
!
! DESCRIPTION
! Module for CO2 dynamics in Sea Water
!
! !INTERFACE
  module mem_CO2
!
! !USES:
  use global_mem
  use SystemForcing, only :ForcingName, ForcingField, FieldInit
  IMPLICIT NONE
!  
!
! !AUTHORS
!   L. Patara, M. Vichi (INGV-CMCC)
!   P. Ruardij, H. Thomas (NIOZ)
!
! !REVISION_HISTORY
!   T. Lovato (CMCC) 2012
! COPYING
!   
!   Copyright (C) 2007 P. Ruardij and M. Vichi
!   (rua@nioz.nl, vichi@bo.ingv.it)
!
!   This program is free software; you can redistribute it and/or modify
!   it under the terms of the GNU General Public License as published by
!   the Free Software Foundation;
!   This program is distributed in the hope that it will be useful,
!   but WITHOUT ANY WARRANTY; without even the implied warranty of
!   MERCHANTEABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!   GNU General Public License for more details.
!
!EOP
!-------------------------------------------------------------------------!
!BOC

  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  ! Default all is public
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  public
  integer,parameter   :: STATIC=1
  integer,parameter   :: DYNAMIC=2
  integer,parameter   :: FOLLOWS=3
  integer,parameter   :: TOTAL=1
  integer,parameter   :: SWS=2
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  ! PelCO2 PARAMETERS (read from nml)
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
   ! Initial Partial pressure in the air
   ! pco2air0    : initial constant value 
   ! calcAtmpCO2 : flag for the computation of Atmospheric pCO2 
   ! AtmCO2      : structure of data for CO2 atmospheric concentration
   ! AtmSLP      : structure of data for atmospheric SLP
   ! AtmTDP      : structure of data for atmospheric Dew Point Temperature
   ! 
   real(RLEN)   :: AtmCO20=365.0_RLEN ! ppm 
   logical      :: calcAtmpCO2=.FALSE. 
   integer      :: pCO2Method=1

   type(ForcingName)    :: AtmCO2_N, AtmSLP_N, AtmTDP_N
   type(ForcingField)   :: AtmCO2, AtmSLP, AtmTDP

   ! Choice of the acidity constants parameterization
   ! K1K2==1 Roy et al. (1993); DOE (1994); pH on total scale
   ! K1K2==2 Default
   !         Mehrbach et al (1973) refit by Dickson & Millero (1987)
   !         OCMIP STANDARD; pH on Sea Water Scale
   ! K1K2==3 Mehrbach et al (1973) refit by Lueker et al. (2000)
   !         pH on total scale
   ! K1K2==4 Hansson (1973b) data as refitted by Dickson and 
   !         Millero (1987); pH on Sea Water Scale
   integer      :: K1K2=2

   ! Choice of [H+] numerical computation
   ! MethodCalcCO2=1 Approximate static solution
   ! MethodCalcCO2=2 Default. Standard OCMIP iteration
   ! MethodCalcCO2=3 Follows et al., Ocean Modelling 2006
   !
   ! Parameters for MethodCalcCO2=2
   ! M2XACC    :  accuracy of the iterative scheme for OCMIP (default 1.E-10)
   ! M2PHDELT  :  delta of pH for the root search (realized pH+/-DELT)
   !              in the OCMIP scheme (default 0.5)
   ! M2MAXIT   :  maximum number of iterations for OCMIP (default 100 )
   !
   integer      :: MethodCalcCO2=2 
   real(RLEN)   :: M2XACC=1.E-20_RLEN
   real(RLEN)   :: M2PHDELT=0.5_RLEN
   integer      :: M2MAXIT=100
   ! Initial pH value (needed for Follows)
   real(RLEN)   :: phstart=8.0_RLEN ! [-]

   ! Choice of pH scale (forced automatically according to K1K2)
   ! phscale = 1: Total
   ! phscale = 2: SeaWater Scale (OCMIP default)
   integer      :: phscale = SWS
   ! Calcium ion concentration 
   ! Ca seawater concentration [mol/m3], from Seawater : Its composition, properties and behaviour"
   !                                              (2nd Edition), Open University Course Team, 1995]
   ! Normalize Calcium ion concentration by sea water salinity 
   real(RLEN)   :: Caconc0 = 10.279E0
   logical      :: Canorm = .TRUE.
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  ! SHARED PUBLIC FUNCTIONS (must be explicited below "contains")
  public InitCO2

  contains

  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  subroutine InitCO2()

#ifdef NOPOINTERS
  use mem
#else
  use mem,          ONLY:EPCO2air,pH,NO_BOXES_XY
#endif
  use api_bfm, ONLY: bfm_init
  use mem_Param, ONLY: slp0
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    namelist /CO2_parameters/ AtmCO20,calcAtmpCO2,pCO2Method,K1K2,MethodCalcCO2,     &
                              phscale,phstart,M2XACC,M2PHDELT,M2MAXIT,         &
                              Caconc0,Canorm,AtmCO2_N,AtmSLP_N,AtmTDP_N
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  integer            ::error=0
  !---------------------------------------------------------------------------
  ! Initialize the structured array that defines if a variable is initialized  
  ! with external data.
  !---------------------------------------------------------------------------
                        ! Read  !   File     ! Netcdf  !  Var   ! File    ! Input      !   Time   !
                        ! Input !   name     ! Logical !  name  ! RefTime ! Frequency  !  interp  !
    AtmCO2_N = ForcingName( 0  , "dummy.nc" , .TRUE.  ,"AtmCO2" , "dummy" ,  "dummy"   ,  .TRUE.  )
    AtmSLP_N = ForcingName( 0  , "dummy.nc" , .TRUE.  ,"AtmSLP" , "dummy" ,  "dummy"   ,  .TRUE.  )
    AtmTDP_N = ForcingName( 0  , 'dummy.nc' , .TRUE.  ,'AtmTDP' , 'dummy' ,  'dummy'   ,  .TRUE.  )

  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  !  Open the namelist file(s)
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

    write(LOGUNIT,*) "#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-"
    write(LOGUNIT,*) "#       BFM Pelagic CO2 SETTINGS                        "
    write(LOGUNIT,*) "#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-"
    write(LOGUNIT,*) "#"
    write(LOGUNIT,*) "#  Reading PelCO2 parameters.."
    open(NMLUNIT,file='CO2.nml',status='old',action='read',err=100)
    read(NMLUNIT,nml=CO2_parameters,err=101)
    close(NMLUNIT)
    write(LOGUNIT,*) "#  Namelist is:"
    write(LOGUNIT,nml=CO2_parameters)
 
  ! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  ! Set initial conditions
  ! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    ! Atmospheric CO2 concentration
    AtmCO2%init = AtmCO2_N%init 
    if (AtmCO2%init == 0) then
       ! Use constant  
       CALL FieldInit(AtmCO2_N, AtmCO2)
       AtmCO2%fnow = AtmCO20
       write(LOGUNIT,*) 'Using constant atmospheric CO2 concentration:', AtmCO2%fnow(1)
       write(LOGUNIT,*) ' '
    else
       ! read external
       CALL FieldInit(AtmCO2_N, AtmCO2)
       write(LOGUNIT,*) 'Using variable atmospheric CO2 concentration. Initial value:', AtmCO2%fnow(1)
       write(LOGUNIT,*) ' '
    endif
    ! Rough approximation: pCO2 is assumed equal to the mixing ratio of CO2
    if (.not. calcAtmpCO2) EPCO2air = AtmCO2%fnow

    ! COMPUTATION OF pCO2
    ! Sea Level Pressure
    AtmSLP%init = AtmSLP_N%init
    AtmTDP%init = AtmTDP_N%init
    if (calcAtmpCO2) then
       if (AtmSLP%init == 0) then
         ! Use constant
          CALL FieldInit(AtmSLP_N, AtmSLP)
          AtmSLP%fnow = slp0
          write(LOGUNIT,*) 'Using constant atmospheric SLP (see slp0 in Param.nml): ', AtmSLP%fnow(1)
          write(LOGUNIT,*) ' '
       else
         CALL FieldInit(AtmSLP_N, AtmSLP)
       endif

       ! Atmospheric Dew Point Temperature
       if (pCO2Method == 2 .AND. AtmTDP%init .ne. 0 ) then
          CALL FieldInit(AtmTDP_N, AtmTDP)
          if (AtmTDP%init .ne. 4 ) &
             write(LOGUNIT,*) 'BFM intialize Dew Point Temperature from file: ', AtmTDP_N%filename
             write(LOGUNIT,*) ' '
       else
          pCO2Method = 1
          write(LOGUNIT,*) 'pCO2Method is forced to 1 beacuse AtmTDP%init is set to 0.'
       endif
    endif

    ! Assign initial pH 
    if (bfm_init /= 1 ) pH(:) = phstart

    ! Check consistency of input parameters
    select case (K1K2)
    case (1) 
        phscale = TOTAL
    case (2) 
        phscale = SWS
    end select
    
    if (calcAtmpCO2) write(LOGUNIT,*) 'BFM computes pCO2 with method: ', pCO2Method
    if (AtmSLP%init == 4 ) write(LOGUNIT,*) 'SLP is provided by external model.' 
    if (AtmTDP%init == 4 ) write(LOGUNIT,*) 'TDP is provided by external model.' 
    write(LOGUNIT,*) "#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-"
    write(LOGUNIT,*)

    FLUSH(LOGUNIT)
    return
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  ! Local Error Messages
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
100 call error_msg_prn(NML_OPEN,"ModuleCO2.f90","CO2.nml")
101 call error_msg_prn(NML_READ,"ModuleCO2.f90","CO2_parameters")
  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  end  subroutine InitCO2

  !-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

  end module mem_CO2

!EOC
!-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
! MODEL  BFM - Biogeochemical Flux Model 
!-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
#endif
