!$Id: bio_save.F90,v 1.5 2005-12-02 20:57:27 hb Exp $
#include"cppdefs.h"
!-----------------------------------------------------------------------
!BOP
! !ROUTINE: Storing the results
!
! !INTERFACE:
   subroutine bio_save(nlev,h,totn)
!
! !DESCRIPTION:
! Here, the output of biogeochemical parameters either as ascii or as
! NetCDF files is managed.
!
! !USES:
   use bio_var
   use output, only: out_fmt,ts
#ifdef BFM_GOTM
   use mem, only:make_flux_output
#endif
#ifdef NETCDF_FMT
   use ncdfout, only: ncid
   use ncdfout, only: lon_dim,lat_dim,z_dim,time_dim,dims
   use ncdfout, only: define_mode,new_nc_variable,set_attributes,store_data
#endif
   IMPLICIT NONE
#ifdef NETCDF_FMT
#include "netcdf.inc"
#endif
!
! !INPUT PARAMETERS:
   integer, intent(in)                 :: nlev
   REALTYPE, intent(in)                :: h(0:nlev)
   REALTYPE, intent(in)                :: totn
!
! !REVISION HISTORY:
!  Original author(s): Hans Burchard & Karsten Bolding
!
! !LOCAL VARIABLES:
   logical, save             :: first=.true.
   integer, save             :: nn
   integer, save             :: totn_id
   integer                   :: iret
   integer                   :: out_unit=67
   REALTYPE                  :: zz
   integer                   :: i,j,n
!EOP
!-----------------------------------------------------------------------
!BOC
   select case (out_fmt)
      case (ASCII)
         if(first) then
            open(out_unit,file='bio.out',status='unknown')
            nn = ubound(cc(1,:),1)
            first = .false.
         end if
         write(out_unit,*)
         write(out_unit,*) trim(ts)
         zz = _ZERO_
         do i=nn,1,-1
            zz=zz+0.5*h(i)
            write(out_unit,115) zz,(cc(j,i) , j=1,numc)
            zz=zz+0.5*h(i)
         end do
115 format(F10.4,100(1x,E10.4E2))

      case (NETCDF)
#ifdef NETCDF_FMT
         if(first) then
            first = .false.

            iret = define_mode(ncid,.true.)
#ifdef BFM_GOTM
            if (bio_setup/=2) then 
#endif
            dims(1) = lon_dim
            dims(2) = lat_dim
            dims(3) = z_dim
            dims(4) = time_dim

#ifdef BFM_GOTM
            do n=stPelStateS,stPelFluxE
               if ( var_ids(n) /= 0 )  then 
#else
            do n=1,numc
#endif
               iret = new_nc_variable(ncid,var_names(n),NF_REAL, &
                                      4,dims,var_ids(n))
               iret = set_attributes(ncid,var_ids(n),       &
                                     units=var_units(n),    &
                                     long_name=var_long(n))
#ifdef BFM_GOTM
               end if
#endif
            end do
#ifdef BFM_GOTM
            end if
#endif

            nn = ubound(cc(1,:),1)

#ifdef BFM_GOTM
          if (bio_setup>1) then                                         !BFM
            dims(1) = lon_dim                                           !BFM
            dims(2) = lat_dim                                           !BFM
            dims(3) = time_dim                                          !BFM
            do n=stBenStateS,stBenFluxE
              if ( var_ids(n) /= 0 )  then                          !BFM
                 iret = new_nc_variable(ncid,var_names(n),NF_REAL, &
                                    3,dims,var_ids(n))               !BFM
                 iret = set_attributes(ncid,var_ids(n),       &
                                   units=var_units(n),    &
                                   long_name=var_long(n))            !BFM
              endif
            end do                                                      !BFM
          end if   
#else
            dims(1) = time_dim
            iret = new_nc_variable(ncid,'totn',NF_REAL,1,dims,totn_id)
            iret = set_attributes(ncid,totn_id,units='mmol/m**2',    &
                   long_name='total N')
#endif

            iret = define_mode(ncid,.false.)
         end if
#ifdef BFM_GOTM
         do n=stPelStateS,stPelStateE
            if ( var_ids(n) > 0 .and. (.not.var_ave(n) )) &
              iret = store_data(ncid,var_ids(n),XYZT_SHAPE,nlev,array=cc(n,:))
         end do
         i=0
         do n=stPelDiagS,stPelDiagE
            i=i+1
            if ( var_ids(n) > 0.and. (.not.var_ave(n) ) ) & 
               iret = store_data(ncid,var_ids(n),XYZT_SHAPE,nlev,array=diag(i,:))
         end do

         i=0
         do n=stPelFluxS,stPelFluxE
            i=i+1
            if ( var_ids(n) > 0  .and. (.not.var_ave(n))) then
               call make_flux_output(1,i,nlev, h, c1dim)
               iret = store_data(ncid,var_ids(n),XYZT_SHAPE,nlev,array=c1dim)
            end if
         end do
         j=0
         do n=stPelStateS,stPelFluxE
            if ( var_ids(n) > 0 .and.var_ave(n) ) then
              j=j+1
              iret = store_data(ncid,var_ids(n),XYZT_SHAPE,nlev,array=cc_ave(j,:))
            endif
         end do

! storage of benthic variables
! temporary storage as scalar: to be modified if benvar are arrays
         if (bio_setup>1) then
           i=0
           do n=stBenStateS,stBenStateE
              i=i+1
              if ( var_ids(n) > 0  .and. (.not.var_ave(n))) &
               iret = store_data(ncid,var_ids(n),XYT_SHAPE,1,scalar=ccb(i,1))
           end do
           i=0
           do n=stBenDiagS,stBenDiagE
             i=i+1
             if ( var_ids(n) > 0  .and. (.not.var_ave(n))) &
               iret = store_data(ncid,var_ids(n),XYT_SHAPE,1,scalar=diagb(i,1))  !BFM
           end do                                                                !BFM
           i=0
           do n=stBenFluxS,stBenFluxE
             i=i+1
             if ( var_ids(n) > 0  .and. (.not.var_ave(n))) then
               call make_flux_output(2,i,nlev, h, c1dim)
               iret = store_data(ncid,var_ids(n),XYT_SHAPE,1,scalar=c1dim(1))  !BFM
             endif
           end do                                                                !BFM
           j=0
           do n=stBenStateS,stBenFluxE
              if ( var_ids(n) > 0 .and. var_ave(n)) then
                 j=j+1
                 iret = store_data(ncid,var_ids(n),XYT_SHAPE,1,scalar=ccb_ave(j,1))  !BFM
              endif
           end do                                                                !BFM
         end if                
#else
         do n=1,numc
            iret = store_data(ncid,var_ids(n),XYZT_SHAPE,nn,array=cc(n,:))
         end do
!KBK         iret = store_data(ncid,phy_id,XYZT_SHAPE,nn,array=cc(2,:)+P0)
!KBK         iret = store_data(ncid,zoo_id,XYZT_SHAPE,nn,array=cc(3,:)+Z0)

         iret = store_data(ncid,totn_id,T_SHAPE,1,scalar=totn)
#endif
#endif
      case default
         FATAL 'A non valid output format has been chosen'
         stop 'bio_save'
   end select

   return
   end subroutine bio_save
!EOC

!-----------------------------------------------------------------------
! Copyright by the GOTM-team under the GNU Public License - www.gnu.org
!-----------------------------------------------------------------------
